buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}

apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

dependencies {
    compile 'org.apache.commons:commons-math3:3.4.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

apply plugin: 'maven'

group = project.GROUP
version = project.VERSION
description = project.DESCRIPTION

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testsJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

def hasBintrayProperties() {
    return project.hasProperty('BINTRAY_USERNAME') && project.hasProperty('BINTRAY_KEY')
}

if (hasBintrayProperties()) {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    bintrayUpload.dependsOn jar, sourcesJar, javadocJar, testsJar
    bintrayUpload.dependsOn("generatePomFileForMavenPublication")

    publish.dependsOn jar, sourcesJar, javadocJar, testsJar
    publish.dependsOn("generatePomFileForMavenPublication")

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                artifact testsJar

                pom.withXml {
                    def Node root = asNode()
                    root.appendNode('name', project.BINTRAY_NAME)
                    root.appendNode('description', project.description)
                    root.appendNode('url', project.URL)

                    def issues = root.appendNode('issueManagement')
                    issues.appendNode('system', 'github')
                    issues.appendNode('url', project.ISSUES_URL)

                    def scm = root.appendNode('scm')
                    scm.appendNode('url', project.SCM)
                    scm.appendNode('connection', project.CONNECTION)
                    scm.appendNode('developerConnection', project.DEV_CONNECTION)

                    def license = root.appendNode('licenses').appendNode('license')
                    license.appendNode('name', 'MIT')
                    license.appendNode('url', 'http://opensource.org/licenses/mit-license.php')
                    license.appendNode('distribution', 'repo')

                    def dev = root.appendNode('developers').appendNode('developer')
                    dev.appendNode('id', project.ORG)
                    dev.appendNode('name', project.ORG)
                    dev.appendNode('organization', project.ORG)
                    dev.appendNode('organizationUrl', project.ORG_URL)
                }
            }

        }
    }

    bintray {
        user = BINTRAY_USERNAME
        key = BINTRAY_KEY
        publications = ['maven']
        publish = true
        pkg {
            repo = 'maven'
            name = project.BINTRAY_NAME
            desc = project.description
            websiteUrl = project.URL
            issueTrackerUrl = project.ISSUES_URL
            vcsUrl = project.SCM
            licenses = ['MIT']
            labels = []
            publicDownloadNumbers = true
            version {
                name = project.version
                desc = project.description
            }
        }
    }
}
